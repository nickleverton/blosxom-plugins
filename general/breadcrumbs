# Blosxom Plugin: breadcrumbs
# Author(s): Rael Dornfest <rael@oreilly.com> 
# Version: 2003-12-29
# Documentation: See the bottom of this file or type: perldoc readme

package breadcrumbs;

# --- Configurable variables -----

# Should I prepend a link back home to the path?
# Specify a word to use for the link back to $blosxom::url or leave blank
# to exclude it

my $home = 'home';

# What divider should I use between path components (e.g. a > path > to...)
my $divider = " :: ";

# True or false: should we show a breadcrumb containing only $home
# when looking at $blosxom::url?  Optional; default is false.
my $home_breadcrumb = 0;

# Code reference which is applied to each path name to make it more
# user friendly.  Example : { ucfirst } to capitalize first letter.
# Value to modify is in $_; change $_ to what should be displayed.
# Optional; set to undef for no action.
#
# Example: Convert each word to upper case, and change - or _ to space:
# my $pretty = sub { my @words = split /[-_]+/;
#                    $_=join(" ", map { ucfirst} @words ) };
my $pretty = sub { my @words = split /[-_]+/;
                   $_=join(" ", map { ucfirst} @words ) };

# --------------------------------

$breadcrumbs; # use as $breadcrumbs::breadcrumbs in flavour templates

use FileHandle;

my $fh = new FileHandle;

sub start {
  1;
}

sub head {
  my($pkg, $path, $head_ref) = @_;

  unless($path) {
    $breadcrumbs = $home if $home && $home_breadcrumb;
    return 0;
  }

  $path =~ s/\.$blosxom::flavour$//;

  my(@p, $p);
  $home and push @p, qq{<a href="$blosxom::url/index.$blosxom::flavour">$home</a>};

  foreach ( split /\//, $path ) {
    $p .= "/$_";

    &$pretty if ref $pretty eq "CODE";
    push @p,
      $p ne "/$path"
      ? qq{<a href="$blosxom::url$p/index.$blosxom::flavour">$_</a>}
      : qq{$_};
  }

  $breadcrumbs = join $divider, @p;

  return 1;
}

1;

__END__

=head1 NAME

Blosxom Plug-in: breadcrumbs

=head1 SYNOPSIS

Populates $breadcrumbs::breadcrumbs with a clickable trail to
your current path in the weblog hierarchy (a la Yahoo!).

e.g. a/path/to/somewhere becomes 
<a href="/a">a</a> :: <a href="/a/path">path</a> :: ...

Optionally prepends the path with a link back to home.  Alter $home
as you please, leaving it blank to turn off the link to home.

=head1 VERSION

2003-12-29

Version number coincides with the version of Blosxom with which the 
current version was first bundled.

=head1 AUTHOR

Rael Dornfest  <rael@oreilly.com>, http://www.raelity.org/

This plugin is now maintained by the Blosxom Sourceforge Team,
<blosxom-devel@lists.sourceforge.net>.

=head1 SEE ALSO

Blosxom Home/Docs/Licensing: http://blosxom.sourceforge.net/

Blosxom Plugin Docs: http://blosxom.sourceforge.net/documentation/users/plugins.html

=head1 BUGS

None known; please send bug reports and feedback to the Blosxom
development mailing list <blosxom-devel@lists.sourceforge.net>.

=head1 LICENSE

Blosxom and this Blosxom Plug-in
Copyright 2003, Rael Dornfest 

Permission is hereby granted, free of charge, to any person obtaining a
copy of this software and associated documentation files (the "Software"),
to deal in the Software without restriction, including without limitation
the rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included
in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
OTHER DEALINGS IN THE SOFTWARE.
